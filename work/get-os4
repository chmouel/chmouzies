#!/usr/bin/env zsh
set -e
# set -x

profile=chmouel
prefixk=os4
operator=
releaseyaml=
koinstall=

TRIGGER_YAML=https://storage.googleapis.com/tekton-releases/triggers/latest/release.yaml
DASHBOARD_YAML=https://github.com/tektoncd/dashboard/releases/download/v0.4.1/dashboard_latest_openshift-tekton-dashboard-release.yaml

BASE_URL=https://uploader-ci-openshift-pipelines.apps.devint.openshiftknativedemo.org/devreinstall/

while getopts ':korR' arg;do
    case $arg in
      (R) reinstall=yes ;;
      (o) operator=yes ;;
      (r) releaseyaml=yes ;;
      (k) koinstall=yes  ;;
      (\?) print invalid option: $OPTARG;exit 1;;
    esac
done

(( OPTIND > 1 )) && shift $(( OPTIND - 1 ))

[[ -n ${1} ]] && { profile=$1; }
[[ -n ${2} ]] && { prefixk=$2; }

function operatorinstall() {
    cat <<EOF | oc apply -f-
apiVersion: operators.coreos.com/v1alpha1
kind: Subscription
metadata:
  name: pipelines
  namespace: openshift-operators
spec:
  channel: dev-preview
  name: tekton-operator
  source: community-operators
  sourceNamespace: openshift-marketplace
EOF
}

function os4_add_htpasswd_auth() {
    oc get secret htpasswd-secret -n openshift-config 2>/dev/null >/dev/null  && return || true

    oc create secret generic htpasswd-secret \
       --from-file=htpasswd=${HOME}/.kube/htpass -n openshift-config
    oc patch oauth cluster -n openshift-config --type merge --patch "spec:
  identityProviders:
  - htpasswd:
      fileData:
        name: htpasswd-secret
    mappingMethod: claim
    name: htpasswd
    type: HTPasswd
"
    oc adm policy add-cluster-role-to-user cluster-admin "$(head ~/.kube/htpass|awk -F: '{print $1}')"
}

function recreate() {
    oc delete -f ${1} 2>/dev/null >/dev/null || true
    oc create -f ${1}
}

function releaseyamlinstall {
    osinstall=https://osinstall.chmouel.com/release/pipeline/nightly.yaml
    curl -sILf -o/dev/null -f ${osinstall} && yaml=${osinstall}


    [[ -z ${yaml} ]] && {
        yaml=https://raw.githubusercontent.com/openshift/tektoncd-pipeline/release-next/openshift/release/tektoncd-pipeline-nightly.yaml
        curl -sILf -o/dev/null -f ${yaml} || {
            stable_version=$(curl -s https://api.github.com/repos/tektoncd/pipeline/releases | python -c "import sys, json;x=json.load(sys.stdin);print(x[0]['tag_name'])")
            yaml=https://raw.githubusercontent.com/openshift/tektoncd-pipeline/release-${stable_version}/openshift/release/tektoncd-pipeline-${stable_version}.yaml
        }
    }
    echo ">>> Installing: $(basename ${yaml})"
    recreate ${yaml}

	echo ">>>  Installing Triggers"
	recreate ${TRIGGER_YAML}

	# echo ">>>  Installing Dashboard"
	# recreate ${DASHBOARD_YAML}
	#oc expose service tekton-dashboard -n tekton-pipelines ; oc apply -n tekton-pipelines -f <(oc get route tekton-dashboard  -o json |jq -r '.spec |= . + {tls: {"insecureEdgeTerminationPolicy": "Redirect", "termination": "edge"}}')
}

function koinstall {
    cd $GOPATH/src/github.com/tektoncd/pipeline

    ${HOME}/bin/git-downup-update-repos

    currentbranch=$(git symbolic-ref -q HEAD);currentbranch=${currentbranch#refs/heads/}
    changed=$(git status --porcelain)
    if [[ -n ${changed} ]];then
        git stash save -a -u "Staged before running an update $(date)"
    fi

    git checkout master

    info() {
        echo "Job has failed you can run"
        echo "  git checkout ${currentbranch}"
        [[ -n ${changed} ]] && echo " and git stash pop";
    }
    trap info ERR

    export GO111MODULE=off
    ko apply -f config/

    git checkout ${currentbranch}

    if [[ -n ${changed} ]];then
        git stash pop
    fi
}


export KUBECONFIG=~/.kube/config.${prefixk}
curl -# -f ${BASE_URL}/${profile}/kubeconfig.gpg | gpg --quiet --decrypt > ${KUBECONFIG}
echo "export KUBECONFIG=${KUBECONFIG}"

os4_add_htpasswd_auth

if [[ -n ${koinstall} ]]; then
    koinstall
elif [[ -n ${releaseyaml} ]];then
    releaseyamlinstall
elif [[ -n ${operator} ]];then
    operatorinstall
fi
