#!/usr/bin/env zsh
local MODE color editwith output CMD file
MODE=fzf

[[ $funcstack[1] == "gg" || "$(basename $0)" == "gg" ]] && {
	MODE=
}

[[ $funcstack[1] == "ge" || "$(basename $0)" == "ge" ]] && {
	MODE=fzf
    editwith=emacs
}

[[ $funcstack[1] == "gv" || "$(basename $0)" == "gv" ]] && {
	MODE=fzf
    editwith=vim
}

if git rev-parse --show-toplevel >/dev/null 2>/dev/null;then
	[[ ${MODE} == fzf ]] && color="--color=never" || color="--color=always"
	CMD="git grep --column --line-number ${=color}"
elif (( $+commands[rg] ));then
	[[ ${MODE} == fzf ]] && color="--no-heading -n --color=never" || color="-p"
	CMD="rg --column ${=color}"
elif (( $+commands[ag] ));then
	CMD="ag --vimgrep"
else
	[[ ${MODE} == fzf ]] && color="--color=never" || color="--color=always"
	CMD="grep -R --line-number ${=color}"
	[[ -d ${@[-1]}  ]] || CMD+=" ."
fi

[[ ${MODE} != fzf ]] && {
	${=CMD} ${@}
	return
}

output=$(${=CMD} ${@} | fzf --delimiter : \
	  -0 \
      --preview 'bat --style=numbers --color=always --highlight-line {2} {1}' \
	  --layout reverse \
      --preview-window 70%:bottom:+{2}-/2)
[[ -z ${output} ]] && return
eval $(echo "${output}"|sed 's|\([^:]*\):\([^:]*\):\([^:]*\):.*|file="\1" line="\2" column="\3"|')
if [[ ${editwith} == vim ]];then
    vim "+normal ${line}G${column}|" "${file}"
elif [[ ${editwith} == emacs ]];then
    emacsclient -n +${line}:${column} "${file}"
else
    echo "${file}"
fi

# vim: ft=zsh
